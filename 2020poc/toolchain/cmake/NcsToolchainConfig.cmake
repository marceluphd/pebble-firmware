# SPDX-License-Identifier: Apache-2.0

# This file provides Zephyr sdk config version package functionality.
#

# Those are Zephyr variables used.
get_filename_component(NCS_TOOLCHAIN_PATH ${CMAKE_CURRENT_LIST_DIR}/../ ABSOLUTE)
if(${CMAKE_HOST_SYSTEM_NAME} STREQUAL Windows)
  set(NCS_TOOLCHAIN_EXECUTABLE_SUFFIX ".exe")
  set(NCS_TOOLCHAIN_BIN_PATH   ${NCS_TOOLCHAIN_PATH}/opt/bin)
  set(NCS_GNUARMEMB_TOOLCHAIN_PATH  ${NCS_TOOLCHAIN_PATH}/opt)
  set(NCS_TOOLCHAIN_ENV_PATH   "${NCS_TOOLCHAIN_BIN_PATH};${NCS_TOOLCHAIN_BIN_PATH}/Scripts;${NCS_TOOLCHAIN_PATH}/mingw64/bin")
  set(NCS_TOOLCHAIN_PYTHONPATH "${NCS_TOOLCHAIN_BIN_PATH}/;${NCS_TOOLCHAIN_BIN_PATH}/Lib;${NCS_TOOLCHAIN_BIN_PATH}/Lib/site-packages")
  set(NCS_TOOLCHAIN_PYTHON     ${NCS_TOOLCHAIN_BIN_PATH}/python.exe)
  set(NCS_TOOLCHAIN_WEST       ${NCS_TOOLCHAIN_BIN_PATH}/Scripts/west.exe)
else()
  set(NCS_TOOLCHAIN_EXECUTABLE_SUFFIX "")
  set(NCS_TOOLCHAIN_BIN_PATH   ${NCS_TOOLCHAIN_PATH}/bin)
  set(NCS_GNUARMEMB_TOOLCHAIN_PATH  ${NCS_TOOLCHAIN_PATH})
  set(NCS_TOOLCHAIN_ENV_PATH   ${NCS_TOOLCHAIN_BIN_PATH})
  set(NCS_TOOLCHAIN_PYTHON     ${NCS_TOOLCHAIN_BIN_PATH}/python3)
  set(NCS_TOOLCHAIN_WEST       ${NCS_TOOLCHAIN_BIN_PATH}/west)
endif()

set(NCS_TOOLCHAIN_CMAKE      ${NCS_TOOLCHAIN_BIN_PATH}/cmake${NCS_TOOLCHAIN_EXECUTABLE_SUFFIX})
set(NCS_TOOLCHAIN_NINJA      ${NCS_TOOLCHAIN_BIN_PATH}/ninja${NCS_TOOLCHAIN_EXECUTABLE_SUFFIX})
set(NCS_TOOLCHAIN_DTC        ${NCS_TOOLCHAIN_BIN_PATH}/dtc${NCS_TOOLCHAIN_EXECUTABLE_SUFFIX})
set(NCS_TOOLCHAIN_GIT        ${NCS_TOOLCHAIN_PATH}/bin/git${NCS_TOOLCHAIN_EXECUTABLE_SUFFIX})
set(NCS_TOOLCHAIN_GPERF      ${NCS_TOOLCHAIN_BIN_PATH}/gperf${NCS_TOOLCHAIN_EXECUTABLE_SUFFIX})

set(NCS_TOOLCHAIN_VARIANT gnuarmemb)

# Those are CMake package parameters.
set(NcsToolchain_FOUND True)

if("${CMAKE_SCRIPT_MODE_FILE}" STREQUAL "${CMAKE_CURRENT_LIST_FILE}")
  file(WRITE ${CMAKE_CURRENT_LIST_DIR}/NcsToolchainPath.txt ${CMAKE_CURRENT_LIST_DIR})
  execute_process(COMMAND ${CMAKE_COMMAND} -E md5sum ${CMAKE_CURRENT_LIST_DIR}/NcsToolchainPath.txt
                  OUTPUT_VARIABLE MD5_SUM
  )
  string(SUBSTRING ${MD5_SUM} 0 32 MD5_SUM)
  if(${CMAKE_HOST_SYSTEM_NAME} STREQUAL Windows)
    execute_process(COMMAND ${CMAKE_COMMAND} -E write_regv
      "HKEY_CURRENT_USER\\Software\\Kitware\\CMake\\Packages\\NcsToolchain\\;${MD5_SUM}" "${CMAKE_CURRENT_LIST_DIR}"
    )
  else()
    file(WRITE $ENV{HOME}/.cmake/packages/NcsToolchain/${MD5_SUM} ${CMAKE_CURRENT_LIST_DIR})
  endif()
  file(REMOVE ${CMAKE_CURRENT_LIST_DIR}/NcsToolchainPath.txt)
endif()
